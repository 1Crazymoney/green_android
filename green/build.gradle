plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.adarshr.test-logger' version '2.1.1'
}

android {
    // BitcoinJ excludes
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org/codehaus/jackson/impl/VERSION.txt'
        exclude 'org/codehaus/jackson/map/VERSION.txt'
        exclude 'org/fusesource/leveldbjni/version.txt'
        exclude 'org/h2/util/data.zip'
        exclude 'META-INF/native/linux32/libleveldbjni.so'
        exclude 'META-INF/native/linux64/libleveldbjni.so'
        exclude 'META-INF/native/osx/libleveldbjni.jnilib'
        exclude 'META-INF/native/windows32/leveldbjni.dll'
        exclude 'META-INF/native/windows64/leveldbjni.dll'
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'com/mysql/jdbc/Charsets.properties'
        exclude 'com/mysql/jdbc/LocalizedErrorMessages.properties'
        exclude 'com/mysql/jdbc/TimeZoneMapping.properties'
        exclude 'com/mysql/jdbc/configs/3-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/5-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/clusterBase.properties'
        exclude 'com/mysql/jdbc/configs/coldFusion.properties'
        exclude 'com/mysql/jdbc/configs/fullDebug.properties'
        exclude 'com/mysql/jdbc/configs/maxPerformance.properties'
        exclude 'com/mysql/jdbc/configs/solarisMaxPerformance.properties'
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 350
        versionName "3.5.0"

        // Stops the Gradle pluginâ€™s automatic rasterization of vectors
        vectorDrawables.useSupportLibrary = true

        testApplicationId "com.blockstream.green.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "normal"
    productFlavors {
        development {
            applicationId "com.greenaddress.greenbits_android_wallet.dev"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "Green Dev"
            resValue "string", "application_id", applicationId
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_dev",
                    appIconRound: "@mipmap/ic_launcher_round_dev"
            ]
        }

        production {
            applicationId "com.greenaddress.greenbits_android_wallet"
            resValue "string", "app_name", "Green"
            resValue "string", "application_id", applicationId
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = 22000000 + android.defaultConfig.versionCode
        }
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            matchingFallbacks = ['normal']
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        ignoreWarnings false
        disable 'MissingTranslation'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

// Enable transforming @AndroidEntryPoint classes in local jvm tests
hilt {
    enableTransformForLocalTests = true
}
// Configure kapt to correct error types by setting correctErrorTypes to true
kapt {
    correctErrorTypes true
}

testlogger {
    theme 'mocha'
}
dependencies {
    /**  --- Modules ---------------------------------------------------------------------------- */
    implementation project(':hardware')
    implementation project(':crypto')
    implementation project(':app')
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Kotlin & KotlinX ------------------------------------------------------------------- */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Android / Google ------------------------------------------------------------------- */
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.biometric:biometric:1.2.0-alpha03"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.browser:browser:1.3.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Lifecycle -------------------------------------------------------------------------- */
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0"
    implementation "androidx.lifecycle:lifecycle-process:2.3.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Navigation ------------------------------------------------------------------------- */
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.3"
    testImplementation "androidx.navigation:navigation-testing:2.3.3"
    androidTestImplementation "androidx.navigation:navigation-testing:2.3.3"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Room ------------------------------------------------------------------------------- */
    implementation "androidx.room:room-runtime:2.3.0-beta03"
    implementation "androidx.room:room-rxjava3:2.3.0-beta03"
    kapt "androidx.room:room-compiler:2.3.0-beta03"
    implementation "androidx.room:room-ktx:2.3.0-beta03"
    testImplementation "androidx.room:room-testing:2.3.0-beta03"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Hilt Dependency Injection  --------------------------------------------------------- */
    implementation "com.google.dagger:hilt-android:2.31.2-alpha"
    kapt 'com.google.dagger:hilt-android-compiler:2.31.2-alpha'

    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt 'androidx.hilt:hilt-compiler:1.0.0-beta01'

    // For instrumentation tests
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.31.2-alpha'
    // androidTestAnnotationProcessor 'com.google.dagger:hilt-android-compiler:1.0.0' // for Java
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.31.2-alpha'

    // For local unit tests
    testImplementation 'com.google.dagger:hilt-android-testing:2.31.2-alpha'
    // testAnnotationProcessor 'com.google.dagger:hilt-android-compiler:1.0.0' // for Java
    kaptTest 'com.google.dagger:hilt-android-compiler:2.31.2-alpha'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- RxJava  ---------------------------------------------------------------------------- */
    implementation "io.reactivex.rxjava3:rxjava:3.0.7"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "com.jakewharton.rxbinding4:rxbinding:4.0.0"
    implementation "com.github.akarnokd:rxjava3-bridge:3.0.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- FastAdapter  ----------------------------------------------------------------------- */
    implementation "com.mikepenz:fastadapter:5.3.1"
    implementation "com.mikepenz:fastadapter-extensions-diff:5.3.1" // diff util helpers
    implementation "com.mikepenz:fastadapter-extensions-binding:5.3.1" // view binding helpers
    implementation "com.mikepenz:fastadapter-extensions-expandable:5.3.1"
    implementation "com.mikepenz:fastadapter-extensions-ui:5.3.1" // pre-defined ui components
    implementation "com.mikepenz:fastadapter-extensions-utils:5.3.1"
    implementation "com.mikepenz:itemanimators:1.1.0"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Logging ---------------------------------------------------------------------------- */
    implementation 'org.slf4j:slf4j-android:1.7.30'
    implementation 'io.github.microutils:kotlin-logging:1.12.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- QR Scanner ------------------------------------------------------------------------- */
    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.4.1'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Bluetooth -------------------------------------------------------------------------- */
    implementation 'com.polidea.rxandroidble2:rxandroidble:1.11.0'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Jackson ---------------------------------------------------------------------------- */
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.6'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.6'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- BottomSheetMenu -------------------------------------------------------------------- */
    implementation 'com.github.Kennyc1012:BottomSheetMenu:3.1.3'
    /** ----------------------------------------------------------------------------------------- */

    testImplementation fileTree(dir: 'libs', include: ['.jar', '.so'])

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

